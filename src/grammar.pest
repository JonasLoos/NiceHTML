comment = _{ "#" ~ (!"\n" ~ ANY)* }  // line comment
w = _{ " "* }  // whitespace

identifier = { ('a'..'z' | 'A'..'Z' | "_" | "-") ~ (ASCII_ALPHANUMERIC | "_" | "-" )* }
string = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }


id_attribute = { "#" ~ identifier }
class_attribute = { "." ~ identifier }
base_attribute = { identifier ~ w ~ "=" ~ w ~ string }
attribute = { id_attribute | class_attribute | base_attribute }
element_name = { identifier ~ (" "+ ~ attribute*)*}
element = { element_name ~ w ~ ("\n" ~ children)? }

definition = { identifier ~ ("(" ~ w ~ identifier ~ (w ~ "," ~ w ~ identifier)* ~ w ~ ")")? ~ w ~ "=" ~ w ~ "\n" ~ children }

string_line = { string }

parameter = _{ string | variable }
variable = { "$" ~ identifier ~ ("(" ~ w ~ parameter ~ (w ~ "," ~ w ~ parameter)* ~ w ~ ")")? }

content = _{ definition | element | variable | string_line | comment | (" " | "\t")* }
first_line = _{ content }
later_line = _{  PEEK_ALL ~ content }
lines = _{ first_line ~ ("\n" ~ later_line)* }

indentation = _{ (" " | "\t")+ }
children = { PEEK_ALL ~ PUSH(indentation) ~ lines ~ DROP }

root = _{ lines ~ EOI }
